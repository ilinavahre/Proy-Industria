
(sentinel::privilege-required "Administrador")

(import "lib/utils" as "utils")
(call "@filters")

(shield::validate
    (shield::field id
        required true
        pattern integer
        check:record_not_found (isnotnull (db::scalar `SELECT user_id FROM ##users WHERE is_active=1 AND user_id={$} {instance_filter}`))
    )
)

(set instance_filter (db::scalar `SELECT instance_id FROM ##users WHERE user_id={id}`))
(set instance_filter (? (instance_filter) " AND x.instance_id=(instance_filter)" " AND x.instance_id IS NULL"))

(shield::validate input

    (shield::field is_authorized
        required false
        pattern bit
    )

    (shield::field username
        required false
        pattern identifier
        check:record_exists (isnull (db::scalar `SELECT user_id FROM ##users x WHERE user_id!={id} AND is_active=1 AND username={!$} {instance_filter}`))
    )

    (shield::field position_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT position_id FROM ##positions x WHERE is_active=1 AND position_id={$} {instance_filter}`))
    )

    (shield::field password
        required false
        set (sentinel::password ($))
    )

    (shield::field name
        required false
        pattern name
    )

    (shield::field email
		presence false
		required true/empty
        pattern email
    )
)

(shield::validate input2
    (shield::field privilege_id
        required:privs false
		pattern integer-list
    )
)

(when (and (eq (id) (session.user.user_id)) (sentinel::has-privilege "Administrador"))
    (when (and (isnotnull (input2.privilege_id)) (isnull (db::scalar `SELECT 1 FROM ##privileges WHERE name='Administrador' AND privilege_id IN ({input2.privilege_id})`)))
        (throw "No se puede quitar el privilegio Administrador del usuario actual.")
    )
)

(db::update "##users" "user_id=(id)" (input))

(when (isnotnull (input2.privilege_id))
	(utils::updateList "user_id" (id) "##user_privileges" "privilege_id" (input2.privilege_id)))

(utils::updatePicture (gateway.request) "photo" (id) "##users" "user_id" "photo" 512 512)

(&)
