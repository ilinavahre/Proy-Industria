
(sentinel::privilege-required "Administrador, Inventario")

(import "lib/utils" as "utils")
(call "@filters")

(shield::begin)

(shield::validate input

    (shield::field instance_id
        set (session.user.instance_id)
    )

    (shield::field created
        set (datetime::now)
    )

    (shield::field is_visible
        default 1
        pattern bit
    )

    (shield::field warehouse_id
        required true
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT warehouse_id FROM ##warehouses x WHERE is_active=1 AND warehouse_id={$} {instance_filter}`))
    )

    (shield::field name
        required true
        requires warehouse_id
        pattern text
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products x WHERE is_active=1 AND name={!$} AND warehouse_id={$out.warehouse_id} {instance_filter}`))
    )

    (shield::field code
        required true
        requires warehouse_id
        pattern code
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products x WHERE is_active=1 AND code={!$} AND warehouse_id={$out.warehouse_id} {instance_filter}`))
    )

    (shield::field unit_id
        required true
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT unit_id FROM ##units x WHERE is_active=1 AND unit_id={$} {instance_filter}`))
    )

    (shield::field tax_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT tax_id FROM ##taxes x WHERE is_active=1 AND tax_id={$} {instance_filter}`))
    )

    (shield::field price
        required true
        pattern positive-numeric
    )
    (shield::field cost
        required true
        pattern positive-numeric
    )

    (shield::field quantity
        required true
        pattern positive-numeric
    )
)

(shield::end)

(db::insert "##products" (input))
(set id (db::lastInsertId))

(utils::updatePicture (gateway.request) "photo" (id) "##products" "product_id" "photo" 512 320)

(&)
