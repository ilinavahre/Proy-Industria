
(sentinel::privilege-required "Administrador, Inventario")

(import "lib/utils" as "utils")

(shield::validate
    (shield::field id
        required true
        pattern integer
        check:record_not_found (isnotnull (db::scalar `SELECT product_id FROM ##products WHERE is_active=1 AND product_id={$}`))
    )
)

(shield::validate input

    (shield::field is_visible
        required false
        pattern bit
    )

    (shield::field warehouse_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT warehouse_id FROM ##warehouses WHERE is_active=1 AND warehouse_id={$}`))
    )

    (shield::field name
        required false
        requires warehouse_id
        pattern text
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products WHERE product_id!={id} AND is_active=1 AND name={!$} AND warehouse_id={$out.warehouse_id}`))
    )

    (shield::field code
        required false
        requires warehouse_id
        pattern code
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products WHERE product_id!={id} AND is_active=1 AND code={!$} AND warehouse_id={$out.warehouse_id}`))
    )

    (shield::field unit_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT unit_id FROM ##units WHERE is_active=1 AND unit_id={$}`))
    )

    (shield::field tax_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT tax_id FROM ##taxes WHERE is_active=1 AND tax_id={$}`))
    )

    (shield::field price
        required false
        pattern positive-numeric
    )
    (shield::field cost
        required false
        pattern positive-numeric
    )

    (shield::field quantity
        required false
        pattern positive-numeric
    )
)

(db::update "##products" "product_id=(id)" (input))

(utils::updatePicture (gateway.request) "photo" (id) "##products" "product_id" "photo" 512 320)

(&)
