
(sentinel::privilege-required "Administrador, Inventario")

(import "lib/utils" as "utils")
(call "@filters")

(shield::validate
    (shield::field id
        required true
        pattern integer
        check:record_not_found (isnotnull (db::scalar `SELECT product_id FROM ##products x WHERE is_active=1 AND product_id={$} {instance_filter}`))
    )
)

(set instance_filter (db::scalar `SELECT instance_id FROM ##products WHERE product_id={id}`))
(set instance_filter (? (instance_filter) " AND x.instance_id=(instance_filter)" " AND x.instance_id IS NULL"))

(shield::validate input

    (shield::field is_visible
        required false
        pattern bit
    )

    (shield::field warehouse_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT warehouse_id FROM ##warehouses x WHERE is_active=1 AND warehouse_id={$} {instance_filter}`))
    )

    (shield::field name
        required false
        requires warehouse_id
        pattern text
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products x WHERE product_id!={id} AND is_active=1 AND name={!$} AND warehouse_id={$out.warehouse_id} {instance_filter}`))
    )

    (shield::field code
        required false
        requires warehouse_id
        pattern code
        check:record_exists (isnull (db::scalar `SELECT product_id FROM ##products x WHERE product_id!={id} AND is_active=1 AND code={!$} AND warehouse_id={$out.warehouse_id} {instance_filter}`))
    )

    (shield::field unit_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT unit_id FROM ##units x WHERE is_active=1 AND unit_id={$} {instance_filter}`))
    )

    (shield::field tax_id
        required false
        pattern integer
        check:invalid (isnotnull (db::scalar `SELECT tax_id FROM ##taxes x WHERE is_active=1 AND tax_id={$} {instance_filter}`))
    )

    (shield::field price
        required false
        pattern positive-numeric
    )
    (shield::field cost
        required false
        pattern positive-numeric
    )

    (shield::field quantity
        required false
        pattern positive-numeric
    )
)

(db::update "##products" "product_id=(id)" (input))

(utils::updatePicture (gateway.request) "photo" (id) "##products" "product_id" "photo" 512 320)

(&)
