; *************************************************************************
(def-fn updateList groupIdField groupId tableName valueField valueList

    (db::exec `UPDATE {tableName} SET tag=1 WHERE {groupIdField}={groupId}`)

    (db::exec `
        INSERT INTO {tableName} ({groupIdField}, {valueField}) VALUES
        {join ',' {map i {split ',' {valueList}} '({groupId}, {i})'}}
        ON DUPLICATE KEY UPDATE tag=0
    `)

    (db::exec `DELETE FROM {tableName} WHERE {groupIdField}={groupId} AND tag=1`)
)

; *************************************************************************
(def-fn updatePicture inputData requestField id tableName idFieldName valueFieldName width height

	(shield::validateData (inputData) formb
		(shield::field (requestField)
			output picture
			required false
			file-type:image png,jpg,jpeg
			max-file-size:16m 16777216
			set (image::load (formData._selfValue.path))
		)
	)

	(if (isnotnull (formb.picture))
		(set x (db::row `SELECT {valueFieldName} as picture FROM {tableName} WHERE {idFieldName}={id}`))

		(if (isnotnull (x.picture))
			(file::remove (x.picture)))

		(set target "storage/(replace '#' '' (tableName))/(id)_(utils::rand).jpg")
		(file::write (target) (image::data (image::smartcut (formb.picture) (width) (height) 0 0 true) jpg))

		(db::exec `UPDATE {tableName} SET {valueFieldName}={!target} WHERE {idFieldName}={id}`)
	)
)

; *************************************************************************
(def-fn setPicture id tableName idFieldName valueFieldName imageData

    (set x (db::row `SELECT {valueFieldName} as picture FROM {tableName} WHERE {idFieldName}={id}`))

    (if (isnotnull (x.picture))
        (file::remove (x.picture)))

    (set target "storage/(replace '#' '' (tableName))/(id)_(utils::rand).jpg")
    (file::write (target) (imageData))

    (db::exec `UPDATE {tableName} SET {valueFieldName}={!target} WHERE {idFieldName}={id}`)
)

; *************************************************************************
(def-fn getPictureUrl path
	(str (gateway.ep) "/" (?? (path) "storage/blank.png"))
)
